-- //////////////////////////////////////////////////////////////
-- // ARCHIVO:			
-- //////////////////////////////////////////////////////////////
-- // BASE DE DATOS:	PYF18_Finanzas
-- // MODULO:			PASIVOS / MP / TABLA AMORTIZACION
-- // OPERACION:		LIBERACION / STORESD PROCEDURE
-- ////////////////////////////////////////////////////////////// 

USE [PYF18_Finanzas_V9999_R0]
GO

-- //////////////////////////////////////////////////////////////






	
-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> SELECT / LISTADO
-- //////////////////////////////////////////////////////////////
-- EXECUTE [dbo].[PG_LI_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]		0,0,0,		29


IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_LI_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_LI_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]
GO


CREATE PROCEDURE [dbo].[PG_LI_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]
	@PP_L_DEBUG				INT,
	@PP_K_SISTEMA_EXE		INT,
	@PP_K_USUARIO_ACCION	INT,
	-- ===========================
	@PP_K_CREDITO_BANCARIO	INT
AS


	DECLARE @VP_MENSAJE				VARCHAR(300)
	DECLARE @VP_L_APLICAR_MAX_ROWS	INT = 1
	
	SET		@VP_MENSAJE		= ''
	
	-- ///////////////////////////////////////////

	IF @VP_MENSAJE=''
		EXECUTE [dbo].[PG_RN_DATA_ACCESO_SELECT]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,	
													11, -- @PP_K_DATA_SISTEMA,	
													@OU_RESULTADO_VALIDACION = @VP_MENSAJE		OUTPUT
	-- ///////////////////////////////////////////

	DECLARE @VP_INT_NUMERO_REGISTROS	INT

	EXECUTE [dbo].[PG_SK_CONFIGURACION_LISTADO_MAX_ROWS_PESADO_GET]		@PP_L_DEBUG, @PP_K_SISTEMA_EXE,
																		@VP_L_APLICAR_MAX_ROWS,		
																		@OU_MAXROWS = @VP_INT_NUMERO_REGISTROS		OUTPUT	
	-- =========================================		
		
	
	DECLARE @VP_L_VER_BORRADOS		[INT]		
	
	EXECUTE [dbo].[PG_RN_DATA_VER_BORRADOS]			@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
													@OU_L_VER_BORRADOS = @VP_L_VER_BORRADOS			OUTPUT
	-- =========================================

	IF @VP_MENSAJE<>''
		SET @VP_INT_NUMERO_REGISTROS = 0

	SELECT	TOP (@VP_INT_NUMERO_REGISTROS) 
			TABLA_AMORTIZACION.*, CREDITO_BANCARIO.*,	
			-- =====================
			RS_ACREDITADA.D_RAZON_SOCIAL	AS D_RAZON_SOCIAL_ACREDITADA, 
			RS_BENEFICIADA.D_RAZON_SOCIAL	AS D_RAZON_SOCIAL_BENEFICIADA, 
			-- =====================
			D_GRUPO_CREDITO, D_ESTATUS_TABLA_AMORTIZACION, D_BANCO, D_MONEDA, 
			S_GRUPO_CREDITO, S_ESTATUS_TABLA_AMORTIZACION, S_BANCO, S_MONEDA, 
			D_USUARIO AS D_USUARIO_CAMBIO		
			-- =====================
	FROM	TABLA_AMORTIZACION, CREDITO_BANCARIO,
			RAZON_SOCIAL AS RS_ACREDITADA, RAZON_SOCIAL AS RS_BENEFICIADA, 
			GRUPO_CREDITO, ESTATUS_TABLA_AMORTIZACION, BANCO, MONEDA, 
			USUARIO
			-- =====================
	WHERE	(	CREDITO_BANCARIO.L_BORRADO=0		OR		@VP_L_VER_BORRADOS=1	)
			-- =====================
	AND		TABLA_AMORTIZACION.K_CREDITO_BANCARIO=CREDITO_BANCARIO.K_CREDITO_BANCARIO
	AND		TABLA_AMORTIZACION.K_ESTATUS_TABLA_AMORTIZACION=ESTATUS_TABLA_AMORTIZACION.K_ESTATUS_TABLA_AMORTIZACION
	AND		CREDITO_BANCARIO.K_RAZON_SOCIAL_ACREDITADA=RS_ACREDITADA.K_RAZON_SOCIAL
	AND		CREDITO_BANCARIO.K_RAZON_SOCIAL_BENEFICIADA=RS_BENEFICIADA.K_RAZON_SOCIAL			
	AND		CREDITO_BANCARIO.K_GRUPO_CREDITO=GRUPO_CREDITO.K_GRUPO_CREDITO
	AND		CREDITO_BANCARIO.K_BANCO=BANCO.K_BANCO	
	AND		CREDITO_BANCARIO.K_USUARIO_CAMBIO=USUARIO.K_USUARIO
	AND		CREDITO_BANCARIO.K_MONEDA=MONEDA.K_MONEDA
	AND		CREDITO_BANCARIO.K_CREDITO_BANCARIO=@PP_K_CREDITO_BANCARIO	
--	ORDER BY D_CREDITO_BANCARIO
	ORDER BY N_PERIODO ASC

	-- /////////////////////////////////////////////////////////////////////

	EXECUTE [dbo].[PG_IN_BITACORA_SYS_OPERACION]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
													-- ===========================================
													2,		-- 0 al 6 // @PP_K_IMPORTANCIA_BITACORA_SYS	[INT],	
													'SELECT',
													@VP_MENSAJE,
													-- ===========================================
													'[PG_LI_TABLA_AMORTIZACION]', -- @PP_STORED_PROCEDURE			[VARCHAR] (100),
													0, 0, 		-- @PP_K_FOLIO_1, @PP_K_FOLIO_2,
													-- === [INT], [INT], [VARCHAR](100), [VARCHAR](100), DECIMAL(19,4), DECIMAL(19,4),
													0, 0, @PP_K_CREDITO_BANCARIO, '' , 0.00, 0.00,
													-- === @PP_VALOR_1 al 6_DATO
													'', '', '@PP_C_TABLA_AMORTIZACION', '', '', ''
														
	-- /////////////////////////////////////////////////////////////////////
GO



-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> SELECT / LISTADO
-- //////////////////////////////////////////////////////////////
-- EXECUTE [dbo].[PG_LI_TABLA_AMORTIZACION]		0,0,0,		29



IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_LI_TABLA_AMORTIZACION]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_LI_TABLA_AMORTIZACION]
GO


CREATE PROCEDURE [dbo].[PG_LI_TABLA_AMORTIZACION]
	@PP_L_DEBUG							INT,
	@PP_K_SISTEMA_EXE					INT,
	@PP_K_USUARIO_ACCION				INT,
	-- ===========================
	@PP_BUSCAR							VARCHAR(200),
	@PP_K_RAZON_SOCIAL_ACREDITADA		INT,
	@PP_K_RAZON_SOCIAL_BENEFICIADA		INT,
	@PP_K_GRUPO_CREDITO					INT,
	@PP_K_ESTATUS_TABLA_AMORTIZACION	INT,
	@PP_K_BANCO							INT,
	@PP_K_MONEDA						INT,
	@PP_TIEMPO_YYYY						INT,
	@PP_TIEMPO_MES						INT
AS

	DECLARE @VP_MENSAJE				VARCHAR(300)
	DECLARE @VP_L_APLICAR_MAX_ROWS	INT = 1
	
	SET		@VP_MENSAJE		= ''
	
	-- ///////////////////////////////////////////

	IF @VP_MENSAJE=''
		EXECUTE [dbo].[PG_RN_DATA_ACCESO_SELECT]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,	
													11, -- @PP_K_DATA_SISTEMA,	
													@OU_RESULTADO_VALIDACION = @VP_MENSAJE		OUTPUT
	-- ///////////////////////////////////////////

	DECLARE @VP_INT_NUMERO_REGISTROS	INT

	EXECUTE [dbo].[PG_SK_CONFIGURACION_LISTADO_MAX_ROWS_PESADO_GET]		@PP_L_DEBUG, @PP_K_SISTEMA_EXE,
																		@VP_L_APLICAR_MAX_ROWS,		
																		@OU_MAXROWS = @VP_INT_NUMERO_REGISTROS		OUTPUT	
	-- =========================================		

	DECLARE @VP_K_FOLIO		INT

	EXECUTE [dbo].[PG_RN_OBTENER_ID_X_REFERENCIA]		@PP_BUSCAR, 
														@OU_K_ELEMENTO = @VP_K_FOLIO	OUTPUT
	-- =========================================
	
	DECLARE @VP_L_VER_BORRADOS		[INT]		
	
	EXECUTE [dbo].[PG_RN_DATA_VER_BORRADOS]			@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
													@OU_L_VER_BORRADOS = @VP_L_VER_BORRADOS			OUTPUT
	-- =========================================

	IF @VP_MENSAJE<>''
		SET @VP_INT_NUMERO_REGISTROS = 0
	
	SELECT	TOP (@VP_INT_NUMERO_REGISTROS) 
			TABLA_AMORTIZACION.*, CREDITO_BANCARIO.*,	
			-- =====================
			RS_ACREDITADA.D_RAZON_SOCIAL	AS D_RAZON_SOCIAL_ACREDITADA, 
			RS_BENEFICIADA.D_RAZON_SOCIAL	AS D_RAZON_SOCIAL_BENEFICIADA, 
			-- =====================
			D_GRUPO_CREDITO, D_ESTATUS_TABLA_AMORTIZACION, D_BANCO, D_MONEDA, 
			S_GRUPO_CREDITO, S_ESTATUS_TABLA_AMORTIZACION, S_BANCO, S_MONEDA, 
			D_USUARIO AS D_USUARIO_CAMBIO		
			-- =====================
	FROM	TABLA_AMORTIZACION, CREDITO_BANCARIO,
			RAZON_SOCIAL AS RS_ACREDITADA, RAZON_SOCIAL AS RS_BENEFICIADA, 
			GRUPO_CREDITO, ESTATUS_TABLA_AMORTIZACION, BANCO, MONEDA, 
			USUARIO
			-- =====================
	WHERE	( CREDITO_BANCARIO.L_BORRADO=0		OR		@VP_L_VER_BORRADOS=1 )
			-- =====================
	AND		TABLA_AMORTIZACION.K_CREDITO_BANCARIO=CREDITO_BANCARIO.K_CREDITO_BANCARIO
	AND		TABLA_AMORTIZACION.K_ESTATUS_TABLA_AMORTIZACION=ESTATUS_TABLA_AMORTIZACION.K_ESTATUS_TABLA_AMORTIZACION
	AND		CREDITO_BANCARIO.K_RAZON_SOCIAL_ACREDITADA=RS_ACREDITADA.K_RAZON_SOCIAL
	AND		CREDITO_BANCARIO.K_RAZON_SOCIAL_BENEFICIADA=RS_BENEFICIADA.K_RAZON_SOCIAL			
	AND		CREDITO_BANCARIO.K_GRUPO_CREDITO=GRUPO_CREDITO.K_GRUPO_CREDITO
	AND		CREDITO_BANCARIO.K_BANCO=BANCO.K_BANCO	
	AND		CREDITO_BANCARIO.K_USUARIO_CAMBIO=USUARIO.K_USUARIO
	AND		CREDITO_BANCARIO.K_MONEDA=MONEDA.K_MONEDA
			-- =====================
	AND		(	@PP_BUSCAR='' 
				OR	D_CREDITO_BANCARIO				LIKE '%'+@PP_BUSCAR+'%' 
				OR	RS_ACREDITADA.D_RAZON_SOCIAL	LIKE '%'+@PP_BUSCAR+'%' 
				OR	RS_BENEFICIADA.D_RAZON_SOCIAL	LIKE '%'+@PP_BUSCAR+'%' 
				OR	GRUPO_CREDITO.D_GRUPO_CREDITO	LIKE '%'+@PP_BUSCAR+'%' 						
				OR	BANCO.D_BANCO					LIKE '%'+@PP_BUSCAR+'%' 
				OR	MONEDA.D_MONEDA					LIKE '%'+@PP_BUSCAR+'%' 
				OR	ESTATUS_TABLA_AMORTIZACION.D_ESTATUS_TABLA_AMORTIZACION	LIKE '%'+@PP_BUSCAR+'%' 					
				-- =====================
				OR	TABLA_AMORTIZACION.K_TABLA_AMORTIZACION=@VP_K_FOLIO 
			)	
			-- =====================
	AND		( @PP_K_RAZON_SOCIAL_ACREDITADA=-1		OR	CREDITO_BANCARIO.K_RAZON_SOCIAL_ACREDITADA=@PP_K_RAZON_SOCIAL_ACREDITADA )
	AND		( @PP_K_RAZON_SOCIAL_BENEFICIADA=-1		OR	CREDITO_BANCARIO.K_RAZON_SOCIAL_BENEFICIADA=@PP_K_RAZON_SOCIAL_BENEFICIADA )
	AND		( @PP_K_GRUPO_CREDITO=-1				OR	CREDITO_BANCARIO.K_GRUPO_CREDITO=@PP_K_GRUPO_CREDITO )
	AND		( @PP_K_BANCO=-1						OR	CREDITO_BANCARIO.K_BANCO=@PP_K_BANCO )
	AND		( @PP_K_MONEDA=-1						OR	CREDITO_BANCARIO.K_MONEDA=@PP_K_MONEDA )
	AND		( @PP_TIEMPO_YYYY=-1					OR	YEAR(TABLA_AMORTIZACION.F_PERIODO_FIN)=@PP_TIEMPO_YYYY )
	AND		( @PP_TIEMPO_MES=-1						OR	MONTH(TABLA_AMORTIZACION.F_PERIODO_FIN)=@PP_TIEMPO_MES )
	AND		( @PP_K_ESTATUS_TABLA_AMORTIZACION=-1	OR	ESTATUS_TABLA_AMORTIZACION.K_ESTATUS_TABLA_AMORTIZACION=@PP_K_ESTATUS_TABLA_AMORTIZACION )
			-- =====================		
	ORDER BY K_TABLA_AMORTIZACION DESC
		
	-- /////////////////////////////////////////////////////////////////////

	EXECUTE [dbo].[PG_IN_BITACORA_SYS_OPERACION]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
													-- ===========================================
													2,		-- 0 al 6 // @PP_K_IMPORTANCIA_BITACORA_SYS	[INT],	
													'SELECT',
													@VP_MENSAJE,
													-- ===========================================
													'[PG_LI_TABLA_AMORTIZACION]', -- @PP_STORED_PROCEDURE			[VARCHAR] (100),
													0, 0, 		-- @PP_K_FOLIO_1, @PP_K_FOLIO_2,
													-- === [INT], [INT], [VARCHAR](100), [VARCHAR](100), DECIMAL(19,4), DECIMAL(19,4),
													0, 0, @PP_BUSCAR, '' , 0.00, 0.00,
													-- === @PP_VALOR_1 al 6_DATO
													'', '', '@PP_C_TABLA_AMORTIZACION', '', '', ''

														
	-- /////////////////////////////////////////////////////////////////////
GO





-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> SELECT / LISTADO
-- //////////////////////////////////////////////////////////////
-- EXECUTE [dbo].[PG_LI_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]		0,0,0,		29


IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_LI_TABLA_AMORTIZACION_DEBUG]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_LI_TABLA_AMORTIZACION_DEBUG]
GO


CREATE PROCEDURE [dbo].[PG_LI_TABLA_AMORTIZACION_DEBUG]
	@PP_L_DEBUG				INT,
	@PP_K_SISTEMA_EXE		INT,
	@PP_K_USUARIO_ACCION	INT,
	-- ===========================
	@PP_K_CREDITO_BANCARIO	INT
AS

	SELECT	
			F_INI.D_TIEMPO_FECHA		AS F_PERIODO_INICIO_DDMMMYYYY,
			F_FIN.D_TIEMPO_FECHA		AS F_PERIODO_FIN_DDMMMYYYY,
			F_LIM.D_TIEMPO_FECHA		AS F_LIMITE_PAGO_DDMMMYYYY,
			TABLA_AMORTIZACION.*
	FROM	TABLA_AMORTIZACION,
			TIEMPO_FECHA F_INI, TIEMPO_FECHA F_FIN, TIEMPO_FECHA F_LIM
	WHERE	TABLA_AMORTIZACION.K_CREDITO_BANCARIO=@PP_K_CREDITO_BANCARIO
	AND		F_INI.F_TIEMPO_FECHA=F_PERIODO_INICIO
	AND		F_FIN.F_TIEMPO_FECHA=F_PERIODO_FIN
	ORDER BY N_PERIODO
	
	-- /////////////////////////////////////////////////////////////////////
GO





-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> SEEK
-- //////////////////////////////////////////////////////////////
-- EXECUTE [dbo].[PG_SK_TABLA_AMORTIZACION]		0,0,0,		29



IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_SK_TABLA_AMORTIZACION]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_SK_TABLA_AMORTIZACION]
GO


CREATE PROCEDURE [dbo].[PG_SK_TABLA_AMORTIZACION]
	@PP_L_DEBUG							INT,
	@PP_K_SISTEMA_EXE					INT,
	@PP_K_USUARIO_ACCION				INT,
	-- ===========================
	@PP_K_TABLA_AMORTIZACION			INT

AS

	DECLARE @VP_MENSAJE				VARCHAR(300)
	DECLARE @VP_L_APLICAR_MAX_ROWS	INT = 1
	
	SET		@VP_MENSAJE		= ''
	
	-- ///////////////////////////////////////////

	IF @VP_MENSAJE=''
		EXECUTE [dbo].[PG_RN_DATA_ACCESO_SELECT]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,	
													11, -- @PP_K_DATA_SISTEMA,	
													@OU_RESULTADO_VALIDACION = @VP_MENSAJE		OUTPUT
	-- ///////////////////////////////////////////
	
	IF @VP_MENSAJE=''
		BEGIN
			SELECT	TABLA_AMORTIZACION.*			
					-- =====================
			FROM	TABLA_AMORTIZACION
					-- =====================
			WHERE	TABLA_AMORTIZACION.K_TABLA_AMORTIZACION=@PP_K_TABLA_AMORTIZACION
		END
	ELSE
		BEGIN
			SELECT	TABLA_AMORTIZACION.*			
					-- =====================
			FROM	TABLA_AMORTIZACION
					-- =====================
			WHERE	TABLA_AMORTIZACION.K_TABLA_AMORTIZACION<0
		END
		
	-- /////////////////////////////////////////////////////////////////////

	EXECUTE [dbo].[PG_IN_BITACORA_SYS_OPERACION]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
													-- ===========================================
													2,		-- 0 al 6 // @PP_K_IMPORTANCIA_BITACORA_SYS	[INT],	
													'SEEK',
													@VP_MENSAJE,
													-- ===========================================
													'[PG_SK_TABLA_AMORTIZACION]', -- @PP_STORED_PROCEDURE			[VARCHAR] (100),
													0, 0, 		-- @PP_K_FOLIO_1, @PP_K_FOLIO_2,
													-- === [INT], [INT], [VARCHAR](100), [VARCHAR](100), DECIMAL(19,4), DECIMAL(19,4),
													0, 0, @PP_K_TABLA_AMORTIZACION, '' , 0.00, 0.00,
													-- === @PP_VALOR_1 al 6_DATO
													'', '', '@PP_K_TABLA_AMORTIZACION', '', '', ''

														
	-- /////////////////////////////////////////////////////////////////////
GO


-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> SELECT / FICHA
-- //////////////////////////////////////////////////////////////


IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_IN_TABLA_AMORTIZACION]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_IN_TABLA_AMORTIZACION]
GO


CREATE PROCEDURE [dbo].[PG_IN_TABLA_AMORTIZACION]
	@PP_L_DEBUG						INT,
	@PP_K_SISTEMA_EXE				INT,
	@PP_K_USUARIO_ACCION			INT,
	-- ===========================
	@PP_K_CREDITO_BANCARIO			INT,
	@PP_N_PERIODO					INT,
	@PP_F_PERIODO_INICIO			DATE,
	@PP_F_PERIODO_FIN				DATE,
	@PP_N_DIAS_PERIODO				INT,
	-- ========================
	@PP_SALDO_INICIAL				DECIMAL(19,4),
	@PP_INTERESES_GENERADOS			DECIMAL(19,4),
	@PP_COMISIONES_BANCO			DECIMAL(19,4),
	@PP_AMORTIZACION_PERIODO		DECIMAL(19,4),
	@PP_SALDO_ACTUALIZADO			DECIMAL(19,4),
	@PP_PAGO_INTERESES				DECIMAL(19,4),
	@PP_PAGO_COMISIONES				DECIMAL(19,4),
	@PP_PAGO_AMORTIZACION			DECIMAL(19,4),
	@PP_PAGO_TOTAL					DECIMAL(19,4),
	@PP_SALDO_FINAL					DECIMAL(19,4),
	@PP_L_EDITADO					INT
AS

	DECLARE @VP_K_ESTATUS_TABLA_AMORTIZACION	INT = 1	-- PENDIENTE

	-- //////////////////////////////////////////////////////////////
	
	DECLARE @VP_K_TABLA_AMORTIZACION			INT

	EXECUTE [dbo].[PG_SK_CATALOGO_K_MAX_GET]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE,
												'TABLA_AMORTIZACION', 
												@OU_K_TABLA_DISPONIBLE = @VP_K_TABLA_AMORTIZACION		OUTPUT

	-- //////////////////////////////////////////////////////////////

	INSERT INTO [TABLA_AMORTIZACION]			
			(	[K_TABLA_AMORTIZACION],
				-- ========================
				[K_CREDITO_BANCARIO], [K_ESTATUS_TABLA_AMORTIZACION],
				[N_PERIODO],
				[F_PERIODO_INICIO], [F_PERIODO_FIN], [N_DIAS_PERIODO],
				-- ========================
				SALDO_INICIAL,
				INTERESES_GENERADOS, COMISIONES_BANCO, AMORTIZACION_PERIODO,
				SALDO_ACTUALIZADO,	PAGO_INTERESES,	PAGO_COMISIONES, 
				PAGO_AMORTIZACION, PAGO_TOTAL, SALDO_FINAL,
				L_VIGENTE, [L_EDITADO], [L_RECALCULAR],
				-- ===========================
				[K_USUARIO_ALTA], [F_ALTA], [K_USUARIO_CAMBIO], [F_CAMBIO]  )
		VALUES	
			(	@VP_K_TABLA_AMORTIZACION,
				-- ===========================
				@PP_K_CREDITO_BANCARIO, @VP_K_ESTATUS_TABLA_AMORTIZACION,
				@PP_N_PERIODO,
				@PP_F_PERIODO_INICIO, @PP_F_PERIODO_FIN, @PP_N_DIAS_PERIODO,
				-- ===========================
				@PP_SALDO_INICIAL,
				@PP_INTERESES_GENERADOS, @PP_COMISIONES_BANCO, @PP_AMORTIZACION_PERIODO,
				@PP_SALDO_ACTUALIZADO, @PP_PAGO_INTERESES, @PP_PAGO_COMISIONES, 
				@PP_PAGO_AMORTIZACION, @PP_PAGO_TOTAL, @PP_SALDO_FINAL,
				1, @PP_L_EDITADO, 0,		-- L_VIGENTE, [L_EDITADO], [L_RECALCULAR],
				-- ===========================
				@PP_K_USUARIO_ACCION, GETDATE(), @PP_K_USUARIO_ACCION, GETDATE() )

	-- //////////////////////////////////////////////////////////////
GO


-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE --->  
-- //////////////////////////////////////////////////////////////

IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_DL_TABLA_AMORTIZACION_X_K_TABLA_AMORTIZACION]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_DL_TABLA_AMORTIZACION_X_K_TABLA_AMORTIZACION]
GO


CREATE PROCEDURE [dbo].[PG_DL_TABLA_AMORTIZACION_X_K_TABLA_AMORTIZACION]
	@PP_L_DEBUG					INT,
	@PP_K_SISTEMA_EXE			INT,
	@PP_K_USUARIO_ACCION		INT,
	-- ===========================
	@PP_K_CREDITO_BANCARIO		INT,
	@PP_N_PERIODO				INT
	-- ===========================
AS
			
	DELETE
	FROM	TABLA_AMORTIZACION
	WHERE	TABLA_AMORTIZACION.K_CREDITO_BANCARIO=@PP_K_CREDITO_BANCARIO
	AND		N_PERIODO>@PP_N_PERIODO

	-- //////////////////////////////////////////////////////////////
GO



-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> SELECT / FICHA
-- //////////////////////////////////////////////////////////////

IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_OP_TABLA_AMORTIZACION_REGENERAR_PERIODOS]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_OP_TABLA_AMORTIZACION_REGENERAR_PERIODOS]
GO


CREATE PROCEDURE [dbo].[PG_OP_TABLA_AMORTIZACION_REGENERAR_PERIODOS]
	@PP_L_DEBUG				INT,
	@PP_K_SISTEMA_EXE		INT,
	@PP_K_USUARIO_ACCION	INT,
	-- ===========================
	@PP_K_CREDITO_BANCARIO		INT,
	@PP_K_TABLA_AMORTIZACION	INT
AS

	IF @PP_L_DEBUG>0
		PRINT	'============================ [PG_OP_TABLA_AMORTIZACION_REGENERAR_PERIODOS]'

	-- =====================================

		DECLARE @VP_MONTO_PRESTAMO				DECIMAL(19,4)
		DECLARE @VP_TASA_INTERES_ANUAL			FLOAT   -- LA TASA SE GUARDA ASI 45.5%=0.455
		DECLARE @VP_CANTIDAD_PERIODOS			INT 
		DECLARE @VP_K_TIPO_CALCULO_CREDITO		INT 
		DECLARE @VP_F_INICIO					DATE

		SELECT	@VP_MONTO_PRESTAMO =			SALDO_FINAL,
				@VP_TASA_INTERES_ANUAL =		TASA_INTERES_ANUAL,
				@VP_CANTIDAD_PERIODOS =			CANTIDAD_PERIODOS,
				@VP_K_TIPO_CALCULO_CREDITO =	K_TIPO_CALCULO_CREDITO,
				@VP_F_INICIO =					F_PERIODO_FIN
												FROM	TABLA_AMORTIZACION,CREDITO_BANCARIO
												WHERE	CREDITO_BANCARIO.K_CREDITO_BANCARIO=@PP_K_CREDITO_BANCARIO
												AND		TABLA_AMORTIZACION.K_CREDITO_BANCARIO=CREDITO_BANCARIO.K_CREDITO_BANCARIO
												AND		TABLA_AMORTIZACION.K_TABLA_AMORTIZACION=@PP_K_TABLA_AMORTIZACION
		-- =======================

		IF @PP_L_DEBUG>0
			BEGIN
			PRINT	'@PP_K_CREDITO_BANCARIO      = '+CONVERT(VARCHAR(20), @PP_K_CREDITO_BANCARIO)
			PRINT	'@VP_MONTO_PRESTAMO          = '+CONVERT(VARCHAR(20), @VP_MONTO_PRESTAMO)
			PRINT	'@VP_TASA_INTERES_ANUAL      = '+CONVERT(VARCHAR(20), @VP_TASA_INTERES_ANUAL)
			PRINT	'@VP_CANTIDAD_PERIODOS       = '+CONVERT(VARCHAR(20), @VP_CANTIDAD_PERIODOS)
			PRINT	'@VP_K_TIPO_CALCULO_CREDITO  = '+CONVERT(VARCHAR(20), @VP_K_TIPO_CALCULO_CREDITO)
			PRINT	'@VP_F_INICIO				 = '+CONVERT(VARCHAR(20), @VP_F_INICIO)
			END


	
	DECLARE @VP_N_PERIODO INTEGER=0
	SELECT	@VP_N_PERIODO=N_PERIODO FROM TABLA_AMORTIZACION
	WHERE	TABLA_AMORTIZACION.K_CREDITO_BANCARIO=@PP_K_CREDITO_BANCARIO
	AND		TABLA_AMORTIZACION.K_TABLA_AMORTIZACION=@PP_K_TABLA_AMORTIZACION

	EXECUTE [DBO].[PG_DL_TABLA_AMORTIZACION_X_K_TABLA_AMORTIZACION]		@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
																		-- ========================
																		@PP_K_CREDITO_BANCARIO,@VP_N_PERIODO

	-- =====================================
		
	DECLARE @VP_0_N_PERIODO							INT 

	DECLARE @VP_1_F_PERIODO_INICIO					DATE
	DECLARE @VP_1_F_PERIODO_FIN						DATE

	DECLARE @VP_2_SALDO_INICIAL						DECIMAL(19,4)
	DECLARE @VP_2_SALDO_FINAL						DECIMAL(19,4)

	DECLARE @VP_3_INTERESES_GENERADOS				DECIMAL(19,4) 
	DECLARE @VP_3_COMISIONES_BANCO					DECIMAL(19,4) = 0
	DECLARE	@VP_3_AMORTIZACION_PERIODO				DECIMAL(19,4)
	DECLARE @VP_3_SALDO_ACTUALIZADO					DECIMAL(19,4)

	DECLARE @VP_4_PAGO_INTERESES					DECIMAL(19,4) = 0
	DECLARE @VP_4_PAGO_COMISIONES					DECIMAL(19,4) = 0
	DECLARE @VP_4_PAGO_AMORTIZACION					DECIMAL(19,4) = 0
	DECLARE @VP_4_PAGO_TOTAL						DECIMAL(19,4)

	DECLARE @VP_L_EDITADO							INT = 1
	
	-- =====================================

	SET @VP_3_AMORTIZACION_PERIODO	= @VP_MONTO_PRESTAMO / (@VP_CANTIDAD_PERIODOS-@VP_N_PERIODO)
	SET @VP_3_AMORTIZACION_PERIODO	= CONVERT(DECIMAL(19,2), @VP_3_AMORTIZACION_PERIODO)
	SET	@VP_4_PAGO_AMORTIZACION		= @VP_3_AMORTIZACION_PERIODO

	IF @PP_L_DEBUG>0
		BEGIN
		PRINT	'@VP_3_AMORTIZACION_PERIODO = '+CONVERT(VARCHAR(20), @VP_3_AMORTIZACION_PERIODO)
		END

	-- ////////////////////////////////////////////////////////////////////////////
	
	DECLARE @VP_N_DIAS_PERIODO				INT
	DECLARE @VP_TASA_INTERES_POR_PERIODO	FLOAT
	DECLARE @VP_FIN							INT		= 0

	-- =====================================

	SET @VP_0_N_PERIODO			= @VP_N_PERIODO+1
	SET @VP_1_F_PERIODO_INICIO	= @VP_F_INICIO
	SET @VP_2_SALDO_INICIAL		= @VP_MONTO_PRESTAMO
	
	WHILE NOT ( @VP_FIN=1 )
		BEGIN
		
		SET @VP_1_F_PERIODO_FIN	= DATEADD(MONTH, 1, @VP_1_F_PERIODO_INICIO) 
		
		-- =====================================

		IF	@VP_K_TIPO_CALCULO_CREDITO=1		-- K_TIPO_CALCULO_CREDITO: #1 AMORT=K | D=30 /// #2 AMORT=K | D=VARIABLE
			SET @VP_N_DIAS_PERIODO = 30 
		ELSE
			SET @VP_N_DIAS_PERIODO = DATEDIFF(DAY, @VP_1_F_PERIODO_INICIO, @VP_1_F_PERIODO_FIN)
	
		-- =====================================
		
		SET @VP_TASA_INTERES_POR_PERIODO	= ( (@VP_TASA_INTERES_ANUAL / 360) * @VP_N_DIAS_PERIODO )

		SET @VP_3_INTERESES_GENERADOS		= @VP_2_SALDO_INICIAL * @VP_TASA_INTERES_POR_PERIODO
		SET @VP_3_INTERESES_GENERADOS		= CONVERT(DECIMAL(19,2), @VP_3_INTERESES_GENERADOS)

		IF @VP_0_N_PERIODO=@VP_CANTIDAD_PERIODOS			-- ////// VALIDACION PARA SALDO CERO EN ULTIMO PERIODO
			BEGIN
				SET @VP_3_AMORTIZACION_PERIODO=@VP_2_SALDO_INICIAL
				SET @VP_4_PAGO_AMORTIZACION = @VP_2_SALDO_INICIAL
			END
		
		SET @VP_3_SALDO_ACTUALIZADO			= @VP_2_SALDO_INICIAL + @VP_3_INTERESES_GENERADOS + @VP_3_COMISIONES_BANCO 
		
		-- =====================================

		SET @VP_4_PAGO_INTERESES	= @VP_3_INTERESES_GENERADOS 

		SET @VP_4_PAGO_TOTAL		= @VP_4_PAGO_INTERESES + @VP_4_PAGO_COMISIONES + @VP_4_PAGO_AMORTIZACION

		--IF @VP_0_N_PERIODO=@VP_CANTIDAD_PERIODOS			-- ////// VALIDACION PARA SALDO CERO EN ULTIMO PERIODO
			
		--	SET @VP_4_PAGO_TOTAL = @VP_3_SALDO_ACTUALIZADO

		SET @VP_2_SALDO_FINAL	= @VP_3_SALDO_ACTUALIZADO - @VP_4_PAGO_TOTAL						
		
		-- =====================================

		IF @PP_L_DEBUG>0
			BEGIN
			PRINT	'@VP_CANTIDAD_PERIODOS			= '+CONVERT(VARCHAR(20), @VP_CANTIDAD_PERIODOS)
			PRINT	'@VP_0_N_PERIODO				= '+CONVERT(VARCHAR(20), @VP_0_N_PERIODO)
			PRINT	'@VP_2_SALDO_INICIAL			= '+CONVERT(VARCHAR(20), @VP_2_SALDO_INICIAL)
			PRINT	'@VP_3_INTERESES_GENERADOS		= '+CONVERT(VARCHAR(20), @VP_3_INTERESES_GENERADOS)
			PRINT	'@VP_3_COMISIONES_BANCO			= '+CONVERT(VARCHAR(20), @VP_3_COMISIONES_BANCO)
			PRINT	'@VP_3_AMORTIZACION_PERIODO		= '+CONVERT(VARCHAR(20), @VP_3_AMORTIZACION_PERIODO)
			PRINT	'@VP_3_SALDO_ACTUALIZADO		= '+CONVERT(VARCHAR(20), @VP_3_SALDO_ACTUALIZADO)
			PRINT	'@VP_4_PAGO_INTERESES			= '+CONVERT(VARCHAR(20), @VP_4_PAGO_INTERESES)
			PRINT	'@VP_4_PAGO_COMISIONES			= '+CONVERT(VARCHAR(20), @VP_4_PAGO_COMISIONES)
			PRINT	'@VP_4_PAGO_AMORTIZACION		= '+CONVERT(VARCHAR(20), @VP_4_PAGO_AMORTIZACION)
			PRINT	'@VP_4_PAGO_TOTAL				= '+CONVERT(VARCHAR(20), @VP_4_PAGO_TOTAL)
			PRINT	'@VP_2_SALDO_FINAL				= '+CONVERT(VARCHAR(20), @VP_2_SALDO_FINAL)
			END

		-- =====================================
	
		EXECUTE [dbo].[PG_IN_TABLA_AMORTIZACION]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
													-- ========================
													@PP_K_CREDITO_BANCARIO,
													@VP_0_N_PERIODO, @VP_1_F_PERIODO_INICIO, @VP_1_F_PERIODO_FIN, @VP_N_DIAS_PERIODO,
													-- ========================
													@VP_2_SALDO_INICIAL,
													@VP_3_INTERESES_GENERADOS, @VP_3_COMISIONES_BANCO, @VP_3_AMORTIZACION_PERIODO,
													@VP_3_SALDO_ACTUALIZADO, @VP_4_PAGO_INTERESES, @VP_4_PAGO_COMISIONES, 
													@VP_4_PAGO_AMORTIZACION, @VP_4_PAGO_TOTAL, @VP_2_SALDO_FINAL, @VP_L_EDITADO													
		-- =====================================
		
		SET @VP_2_SALDO_INICIAL		= @VP_2_SALDO_FINAL

		SET @VP_1_F_PERIODO_INICIO	= @VP_1_F_PERIODO_FIN

		-- =====================================
		
		SET @VP_0_N_PERIODO = @VP_0_N_PERIODO+1

		IF @VP_CANTIDAD_PERIODOS<@VP_0_N_PERIODO
			SET @VP_FIN=1

		END
	
	-- =====================================
GO



-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> 
-- //////////////////////////////////////////////////////////////


IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_UP_TABLA_AMORTIZACION]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_UP_TABLA_AMORTIZACION]
GO


CREATE PROCEDURE [dbo].[PG_UP_TABLA_AMORTIZACION]
    @PP_L_DEBUG                     INT,
    @PP_K_SISTEMA_EXE               INT,
    @PP_K_USUARIO_ACCION			INT,
    -- ===========================
    @PP_K_TABLA_AMORTIZACION		INT,
    -- ========================
    @PP_PAGO_INTERESES				DECIMAL(19,4),
    @PP_PAGO_COMISIONES				DECIMAL(19,4),
    @PP_PAGO_AMORTIZACION			DECIMAL(19,4),
    @PP_COMISIONES_BANCO			DECIMAL(19,4)
AS

    DECLARE @VP_MENSAJE				VARCHAR(300)
       
    SET           @VP_MENSAJE         = ''

    -- ///////////////////////////////////////////

    IF @VP_MENSAJE=''
            EXECUTE [dbo].[PG_RN_TABLA_AMORTIZACION_UPDATE]		@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
                                                                @PP_K_TABLA_AMORTIZACION, 
                                                                @OU_RESULTADO_VALIDACION = @VP_MENSAJE         OUTPUT
    -- ///////////////////////////////////////////

    IF @VP_MENSAJE=''
		BEGIN

		UPDATE TABLA_AMORTIZACION
		SET		COMISIONES_BANCO  = @PP_COMISIONES_BANCO,
				PAGO_COMISIONES   = @PP_PAGO_COMISIONES,
				PAGO_INTERESES        =    @PP_PAGO_INTERESES,
				PAGO_AMORTIZACION = @PP_PAGO_AMORTIZACION
		WHERE  K_TABLA_AMORTIZACION=@PP_K_TABLA_AMORTIZACION
             

		DECLARE	@VP_SALDO_ACTUALIZADO	DECIMAL(19,4)

		SELECT	@VP_SALDO_ACTUALIZADO	=	(	SALDO_INICIAL + INTERESES_GENERADOS 
											+	COMISIONES_BANCO 	)
												FROM	TABLA_AMORTIZACION
												WHERE	K_TABLA_AMORTIZACION=@PP_K_TABLA_AMORTIZACION

        DECLARE	@VP_PAGO_TOTAL	DECIMAL(19,4)
        DECLARE	@VP_SALDO_FINAL	DECIMAL(19,4)
                    
        SET		@VP_PAGO_TOTAL	=	@PP_PAGO_INTERESES + @PP_PAGO_COMISIONES + @PP_PAGO_AMORTIZACION

        SET		@VP_SALDO_FINAL	=	@VP_SALDO_ACTUALIZADO - @VP_PAGO_TOTAL

        UPDATE TABLA_AMORTIZACION
        SET    [PAGO_TOTAL]        =	@VP_PAGO_TOTAL,
            [SALDO_ACTUALIZADO] =	@VP_SALDO_ACTUALIZADO,                         
            [SALDO_FINAL]       =	@VP_SALDO_FINAL,
            [L_EDITADO]			=	1,
            [L_RECALCULAR]      =	1,
            -- ====================
            [F_CAMBIO]			=	GETDATE(), 
            [K_USUARIO_CAMBIO]	=	@PP_K_USUARIO_ACCION
        WHERE  K_TABLA_AMORTIZACION=	@PP_K_TABLA_AMORTIZACION

        END

	-- /////////////////////////////////////////// 

	DECLARE	@VP_K_CREDITO_BANCARIO	INTEGER=0

	SELECT	@VP_K_CREDITO_BANCARIO=	K_CREDITO_BANCARIO 
									FROM TABLA_AMORTIZACION
									WHERE K_TABLA_AMORTIZACION=@PP_K_TABLA_AMORTIZACION
                                                                                              
	EXECUTE [DBO].[PG_OP_TABLA_AMORTIZACION_REGENERAR_PERIODOS]		@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
																	@VP_K_CREDITO_BANCARIO,    @PP_K_TABLA_AMORTIZACION

	-- /////////////////////////////////////////////////////////////////////
       
	IF @VP_MENSAJE<>''
        BEGIN 
             
        SET           @VP_MENSAJE = 'No es posible [Actualizar] la [TablaAmortización]: ' + @VP_MENSAJE 
        SET           @VP_MENSAJE = @VP_MENSAJE + ' ( '
        SET           @VP_MENSAJE = @VP_MENSAJE + '[#Am.'+CONVERT(VARCHAR(10),@PP_K_TABLA_AMORTIZACION)+']'
        SET           @VP_MENSAJE = @VP_MENSAJE + ' )'
             
        END

    -- =============================

    SELECT @VP_MENSAJE AS MENSAJE, @PP_K_TABLA_AMORTIZACION AS CLAVE
       
    -- //////////////////////////////////////////////////////////////
GO




-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE --->  
-- //////////////////////////////////////////////////////////////

IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_DL_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_DL_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]
GO


CREATE PROCEDURE [dbo].[PG_DL_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]
	@PP_L_DEBUG				INT,
	@PP_K_SISTEMA_EXE		INT,
	@PP_K_USUARIO_ACCION	INT,
	-- ===========================
	@PP_K_CREDITO_BANCARIO	INT
	-- ===========================
AS
		
	DELETE
	FROM	TABLA_AMORTIZACION
	WHERE	TABLA_AMORTIZACION.K_CREDITO_BANCARIO=@PP_K_CREDITO_BANCARIO

	-- //////////////////////////////////////////////////////////////
GO



-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> SELECT / FICHA
-- //////////////////////////////////////////////////////////////

IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR_PERIODOS]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR_PERIODOS]
GO


CREATE PROCEDURE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR_PERIODOS]
	@PP_L_DEBUG				INT,
	@PP_K_SISTEMA_EXE		INT,
	@PP_K_USUARIO_ACCION	INT,
	-- ===========================
	@PP_K_CREDITO_BANCARIO		INT,
	@PP_MONTO_PRESTAMO			DECIMAL(19,4),
	@PP_TASA_INTERES_ANUAL		FLOAT,  -- LA TASA SE GUARDA ASI 45.5%=0.455
	@PP_CANTIDAD_PERIODOS		INT, 
	@PP_K_TIPO_CALCULO_CREDITO	INT,
	@PP_F_INICIO				DATE
AS

	IF @PP_L_DEBUG>0
		PRINT	'============================ [PG_OP_TABLA_AMORTIZACION_GENERAR_PERIODOS]'

	-- =====================================
		
	DECLARE @VP_0_N_PERIODO							INT 

	DECLARE @VP_1_F_PERIODO_INICIO					DATE
	DECLARE @VP_1_F_PERIODO_FIN						DATE

	DECLARE @VP_2_SALDO_INICIAL						DECIMAL(19,4)
	DECLARE @VP_2_SALDO_FINAL						DECIMAL(19,4)

	DECLARE @VP_3_INTERESES_GENERADOS				DECIMAL(19,4) 
	DECLARE @VP_3_COMISIONES_BANCO					DECIMAL(19,4) = 0
	DECLARE	@VP_3_AMORTIZACION_PERIODO				DECIMAL(19,4)
	DECLARE @VP_3_SALDO_ACTUALIZADO					DECIMAL(19,4)

	DECLARE @VP_4_PAGO_INTERESES					DECIMAL(19,4) = 0
	DECLARE @VP_4_PAGO_COMISIONES					DECIMAL(19,4) = 0
	DECLARE @VP_4_PAGO_AMORTIZACION					DECIMAL(19,4) = 0
	DECLARE @VP_4_PAGO_TOTAL						DECIMAL(19,4)

	DECLARE @VP_L_EDITADO							INT = 1
	
	-- =====================================

	SET @VP_3_AMORTIZACION_PERIODO	= @PP_MONTO_PRESTAMO / @PP_CANTIDAD_PERIODOS
	SET @VP_3_AMORTIZACION_PERIODO	= CONVERT(DECIMAL(19,2), @VP_3_AMORTIZACION_PERIODO)
	SET	@VP_4_PAGO_AMORTIZACION		= @VP_3_AMORTIZACION_PERIODO

	IF @PP_L_DEBUG>1
		BEGIN
		PRINT	'@VP_3_AMORTIZACION_PERIODO = '+CONVERT(VARCHAR(20), @VP_3_AMORTIZACION_PERIODO)
		END

	-- ////////////////////////////////////////////////////////////////////////////
	
	DECLARE @VP_N_DIAS_PERIODO				INT
	DECLARE @VP_TASA_INTERES_POR_PERIODO	FLOAT
	DECLARE @VP_FIN							INT		= 0

	-- =====================================

	SET @VP_0_N_PERIODO			= 1
	SET @VP_1_F_PERIODO_INICIO	= @PP_F_INICIO
	SET @VP_2_SALDO_INICIAL		= @PP_MONTO_PRESTAMO
	
	WHILE NOT ( @VP_FIN=1 )
		BEGIN
		
		SET @VP_1_F_PERIODO_FIN	= DATEADD(MONTH, 1, @VP_1_F_PERIODO_INICIO) 
		
		-- =====================================

		IF	@PP_K_TIPO_CALCULO_CREDITO=1		-- K_TIPO_CALCULO_CREDITO: #1 AMORT=K | D=30 /// #2 AMORT=K | D=VARIABLE
			SET @VP_N_DIAS_PERIODO = 30 
		ELSE
			SET @VP_N_DIAS_PERIODO = DATEDIFF(DAY, @VP_1_F_PERIODO_INICIO, @VP_1_F_PERIODO_FIN)
	
		-- =====================================
		
		SET @VP_TASA_INTERES_POR_PERIODO	= ( (@PP_TASA_INTERES_ANUAL / 360) * @VP_N_DIAS_PERIODO )

		SET @VP_3_INTERESES_GENERADOS		= @VP_2_SALDO_INICIAL * @VP_TASA_INTERES_POR_PERIODO
		SET @VP_3_INTERESES_GENERADOS		= CONVERT(DECIMAL(19,2), @VP_3_INTERESES_GENERADOS)
		
		SET @VP_3_SALDO_ACTUALIZADO			= @VP_2_SALDO_INICIAL + @VP_3_INTERESES_GENERADOS + @VP_3_COMISIONES_BANCO 
		
		-- =====================================

		SET @VP_4_PAGO_INTERESES	= @VP_3_INTERESES_GENERADOS 

		SET @VP_4_PAGO_TOTAL		= @VP_4_PAGO_INTERESES + @VP_4_PAGO_COMISIONES + @VP_4_PAGO_AMORTIZACION

		IF @VP_0_N_PERIODO=@PP_CANTIDAD_PERIODOS			-- ////// VALIDACION PARA SALDO CERO EN ULTIMO PERIODO
			SET @VP_4_PAGO_TOTAL = @VP_3_SALDO_ACTUALIZADO

		SET @VP_2_SALDO_FINAL	= @VP_3_SALDO_ACTUALIZADO - @VP_4_PAGO_TOTAL						
		
		-- =====================================

		IF @PP_L_DEBUG>0
			BEGIN
			PRINT	'@VP_2_SALDO_INICIAL       = '+CONVERT(VARCHAR(20), @VP_2_SALDO_INICIAL)
			PRINT	'@VP_3_INTERESES_GENERADOS = '+CONVERT(VARCHAR(20), @VP_3_INTERESES_GENERADOS)
			PRINT	'@VP_3_COMISIONES_BANCO    = '+CONVERT(VARCHAR(20), @VP_3_COMISIONES_BANCO)
			PRINT	'@VP_3_SALDO_ACTUALIZADO   = '+CONVERT(VARCHAR(20), @VP_3_SALDO_ACTUALIZADO)
			PRINT	'@VP_4_PAGO_INTERESES      = '+CONVERT(VARCHAR(20), @VP_4_PAGO_INTERESES)
			PRINT	'@VP_4_PAGO_COMISIONES     = '+CONVERT(VARCHAR(20), @VP_4_PAGO_COMISIONES)
			PRINT	'@VP_4_PAGO_AMORTIZACION   = '+CONVERT(VARCHAR(20), @VP_4_PAGO_AMORTIZACION)
			PRINT	'@VP_4_PAGO_TOTAL          = '+CONVERT(VARCHAR(20), @VP_4_PAGO_TOTAL)
			PRINT	'@VP_2_SALDO_FINAL         = '+CONVERT(VARCHAR(20), @VP_2_SALDO_FINAL)
			END

		-- =====================================
	
		EXECUTE [dbo].[PG_IN_TABLA_AMORTIZACION]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
													-- ========================
													@PP_K_CREDITO_BANCARIO,
													@VP_0_N_PERIODO, @VP_1_F_PERIODO_INICIO, @VP_1_F_PERIODO_FIN, @VP_N_DIAS_PERIODO,
													-- ========================
													@VP_2_SALDO_INICIAL,
													@VP_3_INTERESES_GENERADOS, @VP_3_COMISIONES_BANCO, @VP_3_AMORTIZACION_PERIODO,
													@VP_3_SALDO_ACTUALIZADO, @VP_4_PAGO_INTERESES, @VP_4_PAGO_COMISIONES, 
													@VP_4_PAGO_AMORTIZACION, @VP_4_PAGO_TOTAL, @VP_2_SALDO_FINAL, @VP_L_EDITADO													
		-- =====================================
		
		SET @VP_2_SALDO_INICIAL		= @VP_2_SALDO_FINAL

		SET @VP_1_F_PERIODO_INICIO	= @VP_1_F_PERIODO_FIN

		-- =====================================
		
		SET @VP_0_N_PERIODO = @VP_0_N_PERIODO+1

		IF @PP_CANTIDAD_PERIODOS<@VP_0_N_PERIODO
			SET @VP_FIN=1

		END
	
	-- =====================================
GO



-- //////////////////////////////////////////////////////////////
-- // STORED PROCEDURE ---> SELECT / FICHA
-- //////////////////////////////////////////////////////////////
/*
	EXECUTE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]	3, 0, 1,	301

	EXECUTE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]	1, 0, 1,	401
	EXECUTE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]	1, 0, 1,	402
	EXECUTE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]	1, 0, 1,	403
	EXECUTE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]	1, 0, 1,	404
	EXECUTE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]	1, 0, 1,	407

		SELECT * 
		FROM  TABLA_AMORTIZACION 
		WHERE K_CREDITO_BANCARIO=301

		SELECT * 
		FROM  TABLA_AMORTIZACION 
		WHERE K_CREDITO_BANCARIO=403

		SELECT * 
		FROM  TABLA_AMORTIZACION 
		WHERE	N_PERIODO=48

*/


IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]') AND type in (N'P', N'PC'))
	DROP PROCEDURE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]
GO


CREATE PROCEDURE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR]
	@PP_L_DEBUG				INT,
	@PP_K_SISTEMA_EXE		INT,
	@PP_K_USUARIO_ACCION	INT,
	-- ===========================		
	@PP_L_CONTESTA			INT,
	-- ===========================	
	@PP_K_CREDITO_BANCARIO	INT
	-- ===========================
AS
	IF @PP_L_DEBUG>0
		PRINT	'============================ [PG_OP_TABLA_AMORTIZACION_GENERAR]'

	DECLARE @VP_MENSAJE		VARCHAR(300) 
	
	SET		@VP_MENSAJE		= ''

	-- /////////////////////////////////////////////////////////////////////	

	IF @VP_MENSAJE=''
		EXECUTE [dbo].[PG_RN_TABLA_AMORTIZACION_GENERAR]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
															@PP_K_CREDITO_BANCARIO,	
															@OU_RESULTADO_VALIDACION = @VP_MENSAJE		OUTPUT

	-- /////////////////////////////////////////////////////////////////////
	
	IF @VP_MENSAJE=''
		BEGIN

		DECLARE @VP_MONTO_PRESTAMO				DECIMAL(19,4)
		DECLARE @VP_TASA_INTERES_ANUAL			FLOAT   -- LA TASA SE GUARDA ASI 45.5%=0.455
		DECLARE @VP_CANTIDAD_PERIODOS			INT 
		DECLARE @VP_K_TIPO_CALCULO_CREDITO		INT 
		DECLARE @VP_F_INICIO					DATE

		SELECT	@VP_MONTO_PRESTAMO =			MONTO_PRESTAMO,
				@VP_TASA_INTERES_ANUAL =		TASA_INTERES_ANUAL,
				@VP_CANTIDAD_PERIODOS =			CANTIDAD_PERIODOS,
				@VP_K_TIPO_CALCULO_CREDITO =	K_TIPO_CALCULO_CREDITO,
				@VP_F_INICIO =					F_INICIO
												FROM	CREDITO_BANCARIO
												WHERE	CREDITO_BANCARIO.K_CREDITO_BANCARIO=@PP_K_CREDITO_BANCARIO
		-- =======================

		IF @PP_L_DEBUG>0
			BEGIN
			PRINT	'@PP_K_CREDITO_BANCARIO      = '+CONVERT(VARCHAR(20), @PP_K_CREDITO_BANCARIO)
			PRINT	'@VP_MONTO_PRESTAMO          = '+CONVERT(VARCHAR(20), @VP_MONTO_PRESTAMO)
			PRINT	'@VP_TASA_INTERES_ANUAL      = '+CONVERT(VARCHAR(20), @VP_TASA_INTERES_ANUAL)
			PRINT	'@VP_CANTIDAD_PERIODOS       = '+CONVERT(VARCHAR(20), @VP_CANTIDAD_PERIODOS)
			PRINT	'@VP_K_TIPO_CALCULO_CREDITO  = '+CONVERT(VARCHAR(20), @VP_K_TIPO_CALCULO_CREDITO)
			PRINT	'@VP_F_INICIO                = '+CONVERT(VARCHAR(20), @VP_F_INICIO)
			END

		EXECUTE [dbo].[PG_DL_TABLA_AMORTIZACION_X_K_CREDITO_BANCARIO]	@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
																		@PP_K_CREDITO_BANCARIO
		-- =======================

		EXECUTE [dbo].[PG_OP_TABLA_AMORTIZACION_GENERAR_PERIODOS]		@PP_L_DEBUG, @PP_K_SISTEMA_EXE, @PP_K_USUARIO_ACCION,
																		-- ===========================
																		@PP_K_CREDITO_BANCARIO,
																		@VP_MONTO_PRESTAMO, @VP_TASA_INTERES_ANUAL,
																		@VP_CANTIDAD_PERIODOS, @VP_K_TIPO_CALCULO_CREDITO, @VP_F_INICIO																		
		END

	-- /////////////////////////////////////////////////////////////////////
	
	IF @VP_MENSAJE<>''
		BEGIN
		
		SET		@VP_MENSAJE = 'No es posible [Generar] la [TablaAmortización]: ' + @VP_MENSAJE 
		SET		@VP_MENSAJE = @VP_MENSAJE + ' ( '
		SET		@VP_MENSAJE = @VP_MENSAJE + '[#Cr.'+CONVERT(VARCHAR(10),@PP_K_CREDITO_BANCARIO)+']'
		SET		@VP_MENSAJE = @VP_MENSAJE + ' )'
		
		END
	
	IF @PP_L_CONTESTA=1
		SELECT	@VP_MENSAJE AS MENSAJE, @PP_K_CREDITO_BANCARIO AS CLAVE

	-- //////////////////////////////////////////////////////////////
GO







-- ////////////////////////////////////////////////////////////////////////////////////////////////////////////
-- ////////////////////////////////////////////////////////////////////////////////////////////////////////////
-- ////////////////////////////////////////////////////////////////////////////////////////////////////////////
-- ////////////////////////////////////////////////////////////////////////////////////////////////////////////
-- ////////////////////////////////////////////////////////////////////////////////////////////////////////////
